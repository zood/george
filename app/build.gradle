apply plugin: 'com.android.application'

android {
    buildFeatures {
        dataBinding = true
        buildConfig = true
        prefab = true
    }

    defaultConfig {
        applicationId "xyz.zood.george"
        compileSdk = 36
        minSdkVersion 29
        targetSdkVersion 36
        versionCode 65
        versionName "0.65"
        multiDexEnabled = true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        resourceConfigurations += ['en', 'tr', 'ru']
        vectorDrawables.useSupportLibrary = true
        externalNativeBuild {
            cmake {
                cFlags "-Os -fvisibility=hidden"
            }
        }
        ndk {
            abiFilters 'x86_64', 'arm64-v8a', 'x86'
        }
    }
    buildTypes {
        release {
            shrinkResources = false
            minifyEnabled = false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            jniDebuggable = true
        }
    }

    flavorDimensions "channel"
    productFlavors {
        github {
            dimension "channel"
            dependenciesInfo {
                includeInApk = false
            }
            ndk {
                //noinspection ChromeOsAbiSupport
                abiFilters 'arm64-v8a'
            }
        }
        playStore {
            dimension "channel"
            dependenciesInfo {
                includeInApk = true
            }
            ndk {
                //noinspection ChromeOsAbiSupport
                abiFilters 'arm64-v8a', 'armeabi-v7a'
            }
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            outputFileName = "zood-${variant.flavorName}-${variant.versionName}.apk"
        }
    }

    externalNativeBuild {
        cmake {
            path = "CMakeLists.txt"
            version = "4.1.1"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    ndkVersion = '29.0.14206865'
    namespace = 'xyz.zood.george'
    packagingOptions {
        jniLibs {
            useLegacyPackaging = false
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation fileTree(dir: 'libs', include: ['libsodium-1.0.20.0.aar'])

    implementation 'androidx.activity:activity:1.12.0-beta01'
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'androidx.core:core:1.17.0'
    implementation 'com.google.android.material:material:1.13.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'

    // dynamic animation
    implementation 'androidx.dynamicanimation:dynamicanimation:1.1.0'

    // ViewPager2
    implementation "androidx.viewpager2:viewpager2:1.1.0"

    // cloud messaging
    implementation ('com.google.firebase:firebase-messaging:25.0.1') {
        exclude group: 'com.google.firebase', module: 'firebase-core'
        exclude group: 'com.google.firebase', module: 'firebase-analytics'
        exclude group: 'com.google.firebase', module: 'firebase-measurement-connector'
    }

    implementation 'com.google.guava:guava:33.5.0-android'

    implementation 'com.squareup.retrofit2:retrofit:2.12.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.12.0'

//    implementation 'com.squareup.okhttp3:okhttp:3.14.9'
//    implementation 'com.squareup.okhttp3:logging-interceptor:3.12.1'

    implementation 'com.squareup.picasso:picasso:2.5.2'

    implementation 'com.vanniktech:android-image-cropper:4.6.0'
    implementation 'org.maplibre.gl:android-sdk:12.0.1'
    implementation 'org.maplibre.gl:android-plugin-annotation-v9:3.0.2'

    // jetpack libs
    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"
    implementation "androidx.activity:activity:1.11.0"
    implementation "androidx.work:work-runtime:2.10.5"

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.3.0'
    androidTestImplementation 'androidx.test:rules:1.7.0'
}

apply plugin: 'com.google.gms.google-services'

configurations.implementation {
    exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib-jdk8'
}